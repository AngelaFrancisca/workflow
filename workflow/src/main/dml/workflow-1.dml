package module.workflow.domain;

/*
 * @(#)domain_model_workflow_1.dml
 *
 * Copyright 2009 Instituto Superior Tecnico
 * Founding Authors: Jo√£o Figueiredo, Luis Cruz, Paulo Abrantes, Susana Fernandes
 * 
 *      https://fenix-ashes.ist.utl.pt/
 * 
 *   This file is part of the MyOrg web application infrastructure.
 *
 *   MyOrg is free software: you can redistribute it and/or modify
 *   it under the terms of the GNU Lesser General Public License as published
 *   by the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.*
 *
 *   MyOrg is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 *   GNU Lesser General Public License for more details.
 *
 *   You should have received a copy of the GNU Lesser General Public License
 *   along with MyOrg. If not, see <http://www.gnu.org/licenses/>.
 * 
 */

valueType pt.utl.ist.fenix.tools.util.Strings as Strings {
    externalizeWith {
        String exportAsString();
    }
    internalizeWith pt.utl.ist.fenix.tools.util.Strings.importFromString();
}

class WorkflowSystem {
}

class WorkflowProcess {
	String processNumber;
}

class WorkflowLog {
	DateTime whenOperationWasRan;
	Strings descriptionArguments;
}

	class ActivityLog extends WorkflowLog {
		String operation;
	}

	class LabelLog extends WorkflowLog {
		String label;
		String bundle;
	}

	class FileUploadLog extends WorkflowLog {
	}

	class FileRemoveLog extends WorkflowLog {
	}
	
	class FileAccessLog extends WorkflowLog {
	}

class WorkflowProcessComment {
	String comment;
	DateTime date;
} 

class WorkflowQueue {
	String name;
} 

	class WorkflowUserGroupQueue extends WorkflowQueue {
	}

relation WorkflowflowSystemWorkflowProcess {
	WorkflowSystem playsRole workflowSystem {
		multiplicity 1..1;
	}
	WorkflowProcess playsRole processes {
		multiplicity *;
	}
}

relation WorkflowflowSystemComment {
	WorkflowSystem playsRole workflowSystem;
	WorkflowProcessComment playsRole processComments {
		multiplicity *;
	}
}

relation WorkflowflowSystemWorkflowLog {
	WorkflowSystem playsRole workflowSystem {
		multiplicity 1..1;
	}
	WorkflowLog playsRole processLogs {
		multiplicity *;
	}
}

relation WorkflowProcessActivityLog {
	WorkflowLog playsRole executionLogs {
		multiplicity *;
	}
	WorkflowProcess playsRole process {
		multiplicity 1..1;
	}
}

relation WorkflowProcessWorkflowProcessComment {
	WorkflowProcessComment playsRole comments {
		multiplicity *;
	}	
	WorkflowProcess playsRole process;
}

relation WorkflowProcessesWorkflowQueues {
	WorkflowProcess playsRole processes {
		multiplicity *;
	}	
	WorkflowQueue playsRole currentQueues {
		multiplicity *;
	}
}

relation QueueHistory {
	WorkflowProcess playsRole processesHistory {
		multiplicity *;
	}	
	WorkflowQueue playsRole queueHistory {
		multiplicity *;
	}
}

relation WorkflowSystemWorkflowQueue {
	WorkflowSystem playsRole workflowSystem {
		multiplicity 1..1;
	}
	WorkflowQueue playsRole workflowQueues {
		multiplicity *;
	}
}

relation VirtualHostWorkflowSystem {
	protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	WorkflowSystem playsRole workflowSystem;
}
